MATCH (e:EstacaoMeteorologica)-[d:Dados_Meteorologicos]->(:Cidade)
WHERE d.temperatura_max IS NOT NULL AND d.temperatura_min IS NOT NULL

WITH e, date(d.data_horario_coleta) AS dia, 
    max(d.temperatura_max) AS temp_max_dia, 
    min(d.temperatura_min) AS temp_min_dia
WITH e, dia, temp_max_dia, temp_min_dia, (temp_max_dia - temp_min_dia) AS variacao_diaria

ORDER BY variacao_diaria DESC

WITH e,
    avg(variacao_diaria) AS media_variacao,
    collect({
        dia: dia, 
        variacao_diaria: variacao_diaria, 
        temp_max: temp_max_dia, 
        temp_min: temp_min_dia
    }) AS variacoes_ordenadas
WITH e, media_variacao, variacoes_ordenadas[0] AS dia_maior_variacao

MATCH (e:EstacaoMeteorologica)-[:Pertence]->(c:Cidade)-[:Pertence]->(s:Estado)

RETURN
    e.nome AS estacao,
    c.nome AS cidade,
    s.uf AS estado,
    dia_maior_variacao.dia AS dia_maior_variacao,
    dia_maior_variacao.variacao_diaria AS maior_variacao,
    dia_maior_variacao.temp_max AS temperatura_max,
    dia_maior_variacao.temp_min AS temperatura_min,
    round(media_variacao, 2) AS media_variacao

ORDER BY maior_variacao DESC